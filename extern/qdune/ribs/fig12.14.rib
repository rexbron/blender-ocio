#########################################################################
# fig12.14.rib - RIB to create Figure 12.14 of:
#
# _Advanced RenderMan: Creating CGI for Motion Picture_, 
# by Anthony A. Apodaca and Larry Gritz, Morgan Kaufmann, 1999.
#
# This file demostrates the lensflare shader.
# The lights in this file use a shadow map, which can be generated 
# by rendering smokesm before rendering this file.
#########################################################################

Display "fig12.14" "framebuffer" "rgb"
#Display "fig12.14.tga" "file" "rgb"
Format 640 480 1
#Format 1024 768 1
Exposure 1 2.0
PixelSamples 2 2
Clipping .1 10000
ShadingRate 1
Projection "perspective" "fov" [18]
Translate 0 0 3
Rotate -90 1 0 0
WorldBegin
Translate -0.9 0 -0.015
LightSource "ambientlight" 0 "intensity" 0.025

#Attribute "light" "shadows" ["on"]
LightSource "platlight" 1 "from" [2 -4  1] "to" [ 0 0 0] "intensity" [.8]
        "float integstart" [0] "float integend" [4] "float k" [70]
	"float maxstepsize" [1] "float minstepsize" [0.01]
	"float falloff" [600] "float density" [1]
Attribute "light" "shadows" "off"
LightSource "distantlight" 2 "from" [1 -8  1] "to" [ 0 0 0] "intensity" [.8] 
         "lightcolor" [1 1 0.8]
Illuminate 2 0
AttributeBegin
Translate 100 -800 100
Color 1 0 0
AttributeEnd

AttributeBegin
Illuminate 2 1
Illuminate 1 0
Surface "lensflare" "float seed" [5]
CoordSysTransform "camera"
Translate 0 0 0.11
Scale 2 2 2
Patch "bilinear" "P" [ -1 1 0  1 1 0   -1 -1 0  1 -1 0]
AttributeEnd

Atmosphere "planetatmo" "float integstart" [0] "float integend" [4] "float k" [70] "float maxstepsize" [.5] "float minstepsize" [0.01]
           "float falloff" [400] "float density" [10] "float use_lighting" [1] "float Klight" [2.5]

AttributeBegin
Rotate 30 0 0 1
AttributeBegin
Surface "terran" "float bump_scale" [0.000] "float spectral_exp" [0.75]
Color [0.75 0.75 0.75]
Sphere 1 -1 1 360
AttributeEnd

Surface "planetclouds" "float offset" [.27] "Kd" [1]
Scale 1.0012 1.0012 1.0012
Attribute "render" "casts_shadows" "shade"
Attribute "render" "casts_shadows" "none"
Sphere 1 -1 1 360
AttributeEnd

AttributeBegin 
LightSource "ambientlight" 4 "intensity" [0.025]
Color [ 0 0 0 ]
Surface "starfield" "float pswidth" [.23] "float frequency" [30] "intensity" [10]
Attribute "render" "casts_shadows" "none"
Sphere 1000 -1000 1000 360
AttributeEnd
WorldEnd

